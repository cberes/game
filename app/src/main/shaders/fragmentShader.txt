#version 130

uniform vec3 lightColor;
uniform float reflectivity;
uniform float shineDamper;
uniform sampler2D textureSampler;

// input is output from the vertex shader
in vec3 toCameraVector;
in vec3 surfaceNormal;
in vec3 toLightVector;
in vec2 pass_textureCoords;

out vec4 out_Color;

void main(void) {
	vec3 unitCameraVector = normalize(toCameraVector);
	vec3 unitNormal = normalize(surfaceNormal);
	vec3 unitLightVector = normalize(toLightVector);

	// diffuse lighting
	float nDot1 = dot(unitNormal, unitLightVector);
	float brightness = max(nDot1, 0.2); // use 0.2 instead of 0.0 to add ambient lighting
	vec3 diffuse = brightness * lightColor;

	// specular lighting
	vec3 lightDirection = -unitLightVector;
	vec3 reflectedLightDirection = reflect(lightDirection, unitNormal);
	float nDot2 = dot(reflectedLightDirection, unitCameraVector);
	float specularFactor = max(nDot2, 0.0);
	float dampedFactor = pow(specularFactor, shineDamper);
	vec3 specular = reflectivity * dampedFactor * lightColor;

	// simple means of transparency useful for objects that are either fully transparent or fully opaque
	// but apparently it's not efficient?
	vec4 textureColor = texture(textureSampler, pass_textureCoords);
	if (textureColor.a < 0.5) {
		discard;
	}

	out_Color = vec4(diffuse, 1.0) * textureColor + vec4(specular, 1.0);
}